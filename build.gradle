apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'maven-publish'


version = '0.1.6'
description = 'LBU-BB-UploadMonitor'
group = 'uk.ac.leedsbeckett'

def bbhandle = 'bbuploadmonitor'
def bbname = 'LBU-BB-Upload-Monitor'
def bbdescription = 'LBU BB Upload Monitor'
def vendorid = 'LBU'
def vendorname = 'Leeds Beckett University'
def vendordescription = 'Leeds Beckett University'
def vendorurl = 'http://www.leedsbeckett.ac.uk/'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}


clean {
    delete "${buildDir}/webappconfig"
}

war.doFirst {
  copy {
    from "src/main/webappconfig"
    into "${buildDir}/webappconfig"
    expand([
     bbhandle: bbhandle,
     bbname: bbname,
     version: version,
     vendorid: vendorid,
     vendorname: vendorname,
     vendordescription: vendordescription,
     vendorurl: vendorurl,
     description: bbdescription
    ])
  }
}
war {
  webInf { from "${buildDir}/webappconfig" }
}

repositories {
    jcenter()
    maven {
      url "https://maven.blackboard.com/content/repositories/releases/"
    }    
    maven {
      url "https://maven.pkg.github.com/leeds-beckett-digital-learning/lbu-bb-b2-utils"
          credentials {
              username = gitHubUserName
              password = gitHubPrivateToken // the variable resides in ~/.gradle/gradle.properties
          }
    }
}

dependencies {
    compileOnly "javax.servlet:javax.servlet-api:3.1.0"
    compileOnly "blackboard.platform:bb-platform:3900.0.0"
    compileOnly "blackboard.platform:xsscore:3900.0.0"
    compileOnly "log4j:log4j:1.2.17"
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.10.0'    
    compileOnly( "javax.jms:jms:1.1" )
    compileOnly "com.sun.mail:javax.mail:1.6.2"
    // compileOnly fileTree(dir: 'locallibs', include: '*.jar')
    
    // For StringUtils.isEmpty()
    implementation 'org.apache.commons:commons-text:1.9'
    
    implementation 'uk.ac.leedsbeckett:bbb2utils:0.1.16'
    
    testImplementation 'junit:junit:4.12'
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url "https://maven.pkg.github.com/leeds-beckett-digital-learning/uploadmonitor"
            credentials {
                username = gitHubUserName
                password = gitHubPrivateToken // the variable resides in ~/.gradle/gradle.properties
            }
        }
    }
}
